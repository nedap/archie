archetype (adl_version=2.0.7; rm_release=1.0.2)
	openEHR-EHR-COMPOSITION.VRANP_annotations_wrong_rm_path.v1.0.0

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["name"] = <"Thomas Beale">
		["organisation"] = <"openEHR">
		["email"] = <"thomas.beale@oceaninformatics.com">
		["date"] = <"9/11/2011">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Annotation on non-existent (mis-spelled) RM path">
			copyright = <"Â©2011 openEHR Foundation">
		>
	>
	lifecycle_state = <"unmanaged">
	other_details = <
		["regression"] = <"VRANP">
	>

definition
	COMPOSITION[id1] matches {	-- Prescription
		category matches {
			DV_CODED_TEXT[id16] matches {
				defining_code matches {[at1]}
			}
		}
		context matches {
			EVENT_CONTEXT[id17] matches {
				other_context matches {
					ITEM_TREE[id2] matches {
						items matches {
							CLUSTER[id3] occurrences matches {0..*} matches {	-- Qualification
								items matches {
									ELEMENT[id4] occurrences matches {0..1} matches {	-- OrderID
										value matches {
											DV_EHR_URI[id15] 
											DV_IDENTIFIER[id18] 
										}
									}
									ELEMENT[id5] occurrences matches {0..*} matches {	-- Endorsement
										value matches {
											DV_TEXT[id19] 
										}
									}
									ELEMENT[id6] occurrences matches {0..*} matches {	-- AuthorisationID
										value matches {
											DV_IDENTIFIER[id20] 
										}
									}
									ELEMENT[id7] occurrences matches {0..*} matches {	-- Comment
										value matches {
											DV_TEXT[id21] 
										}
									}
								}
							}
						}
					}
				}
			}
		}
		content cardinality matches {1..*; unordered} matches {
			allow_archetype INSTRUCTION[id8] occurrences matches {1..*} matches {	-- Medication instruction
				include
					archetype_id/value matches {/openEHR-EHR-INSTRUCTION\.medication\.v1/}
			}
			allow_archetype ENTRY[id9] occurrences matches {0..*} matches {	-- Other data
				include
					archetype_id/value matches {/.*/}
			}
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1"] = <
				text = <"Prescription">
				description = <"A document authorising supply and administration of one or more medicines, vaccines or other therapeutic goods (as a collection of medication instrations) to be communicated to a dispensing or administration provider.">
			>
			["id3"] = <
				text = <"Qualification">
				description = <"Qualifications on any medication order included in the prescription.">
			>
			["id4"] = <
				text = <"OrderID">
				description = <"The path or ID of the order referenced. If there is no OrderID then the endorsement relates to the entire prescription.">
			>
			["id5"] = <
				text = <"Endorsement">
				description = <"Asserting that a special condition applies such as approval for  enhanced subsidy or concurrent supply. Australian examples include Regulation 24 (PBS), Hardship Conditions (RPBS) or CTG for 'close the gap'.">
			>
			["id6"] = <
				text = <"AuthorisationID">
				description = <"An identifier authorising prescription, dispensing or reimbursement for this medication order.">
			>
			["id7"] = <
				text = <"Comment">
				description = <"Comment on any qualification.">
			>
			["id8"] = <
				text = <"Medication instruction">
				description = <"Contains one or more medication instructions to be supplied.">
			>
			["id9"] = <
				text = <"Other data">
				description = <"Other observational or relevant data.">
			>
			["at1"] = <
				text = <"event">
				description = <"event">
			>
		>
	>
	term_bindings = <
		["openehr"] = <
			["at1"] = <http://openehr.org/id/433>
		>
	>

annotations
	documentation = <
		["en"] = <
			["/context[id17]/health_crae_facility/name"] = <
				["design note"] = <"Note on use of non-archteyped context/health_care_facility/name RM element in this data">
			>
		>
	>
