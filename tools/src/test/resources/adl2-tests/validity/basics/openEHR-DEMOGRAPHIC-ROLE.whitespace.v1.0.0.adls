archetype (adl_version=2.0.7; rm_release=1.0.2)
	openEHR-DEMOGRAPHIC-ROLE.whitespace.v1.0.0

language
	original_language = <[ISO_639-1::pt-br]>
	translations = <
		["en"] = <
			language = <[ISO_639-1::en]>
			author = <
				["name"] = <"Sergio Miranda Freire">
				["organisation"] = <"Universidade do Estado do Rio de Janeiro - UERJ">
				["email"] = <"sergio@lampada.uerj.br">
			>
		>
	>

description
	original_author = <
		["name"] = <"Sergio Miranda Freire & Rigoleta Dutra Mediano Dias">
		["organisation"] = <"Universidade do Estado do Rio de Janeiro - UERJ">
		["email"] = <"sergio@lampada.uerj.br">
		["date"] = <"22/05/2009">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Representation of an organisations's demographic data in the role of healthcare provider">
			use = <"Used in demographic service to collect demographic data about an organisation in the role of a healthcare provider">
			keywords = <"demographic service", "healthcare provider organisation">
		>
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			purpose = <"Representação dos dados de uma Organização ao exercer o papel de um prestador de assistência à saúde">
			use = <"Usado em serviços de demografia para coletar dados demográficos sobre uma organização no papel de prestador de assistência à saúde">
			keywords = <"serviço demográfico", "organização prestadora de assistência à saúde">
		>
	>
	lifecycle_state = <"unmanaged">
	copyright = <"© openEHR Foundation">
	other_details = <
		["regression"] = <"PASS">
	>

definition
	ROLE[id1] matches {	-- Healthcare Provider Organisation
		details matches {
			ITEM_TREE[id2] occurrences matches {0..1} matches {	-- Identifiers
				items cardinality matches {1..*; unordered; unique} matches {
					allow_archetype CLUSTER[id11] occurrences matches {1..*} matches {	-- Identifier
						include
							archetype_id/value matches {/(provider_identifier)a-zA-Z0-9_-]*\.v1/}
					}
				}
			}
		}
		identities cardinality matches {1..*; ordered} matches {
			allow_archetype PARTY_IDENTITY[id3] occurrences matches {1..*} matches {	-- Provider name
				include
					archetype_id/value matches {/(organisation_name)a-zA-Z0-9_-]*\.v1/}
			}
		}
		relationships matches {
			PARTY_RELATIONSHIP[id4] occurrences matches {1} matches {	-- Third-party payer relationship
				details matches {
					ITEM_TREE[id31] occurrences matches {0..1} matches {	-- Details
						items cardinality matches {1..*; ordered} matches {
							allow_archetype CLUSTER[id32] occurrences matches {0..1} matches {	-- identifier used in the relationship
								include
									archetype_id/value matches {/(provider_identifier)a-zA-Z0-9_-]*\.v1/}
							}
						}
					}
				}
			}
		}
	}

terminology
	term_definitions = <
		["pt-br"] = <
			["id1"] = <
				text = <"Prestador de assistência à saúde">
				description = <"Organização no papel de um prestador de assistência à saúde">
			>
			["id2"] = <
				text = <"Identificadores">
				description = <"Identificadores utilizados pela organização quando atua como prestador de assistência à saúde">
			>
			["id3"] = <
				text = <"Nome do prestador">
				description = <"Especifica como a organização prestadora de assistência à saúde é conhecida">
			>
			["id4"] = <
				text = <"Relacionamento com fonte pagadora">
				description = <"Relacionamento entre a organização prestadora de assistência e uma fonte pagadora">
			>
			["id11"] = <
				text = <"Identificador">
				description = <"Identificador utilizado pelo prestador de assistência à saúde">
			>
			["id31"] = <
				text = <"Detalhes">
				description = <"Detalhes do relacionamento">
			>
			["id32"] = <
				text = <"Identificador">
				description = <"Identificador utilizado pelo prestador no relacionamento com a fonte pagadora">
			>
		>
		["en"] = <
			["id1"] = <
				text = <"Healthcare Provider Organisation">
				description = <"Organisation in the role of a healthcare provider">
			>
			["id2"] = <
				text = <"Identifiers">
				description = <"The identifiers used by the organisation acting as a healthcare provider organisation">
			>
			["id3"] = <
				text = <"Provider name">
				description = <"Indicates how the healthcare provider organisation is known by">
			>
			["id4"] = <
				text = <"Third-party payer relationship">
				description = <"relationship between the healthcare provider organisation and a third-party payer">
			>
			["id11"] = <
				text = <"Identifier">
				description = <"The identifier used by the organisation in the role of a healthcare provider organisation">
			>
			["id31"] = <
				text = <"Details">
				description = <"Relationship details">
			>
			["id32"] = <
				text = <"identifier used in the relationship">
				description = <"Identifier used by the healthcare provider in the relationship with the third-party payer">
			>
		>
	>
