archetype (adl_version=2.0.5; rm_release=1.0.2)
	openEHR-EHR-OBSERVATION.flattening_parent_1.v1.0.0

language
	original_language = <[ISO_639-1::en]>

description
	custodian_namespace = <"org.openehr">
	custodian_organisation = <"openEHR Foundation <http://www.openEHR.org>">

	original_author = <
		["name"] = <"Thomas Beale">
 		["email"] = <"thomas.beale@openEHR.org">    
		["organisation"] = <"openEHR Foundation <http://www.openEHR.org>">
		["date"] = <"2009-05-06">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Overwrite parent 'matches-any' node (id13) with 2 child nodes.">
			keywords = <"ADL", "flattening", "test">
		>
	>
	lifecycle_state = <"published">
	other_details = <
		["regression"] = <"PASS">
	>
	copyright = <"Copyright Â© 2009 openEHR Foundation <http://www.openEHR.org>">
	licence = <"Creative Commons CC-BY-SA <https://creativecommons.org/licenses/by-sa/3.0/>">

definition
	OBSERVATION[id1] matches {	-- Substance Use
		data matches {
			HISTORY[id2] matches {
				events cardinality matches {1..*; unordered} matches {
					EVENT[id3] occurrences matches {0..1} matches {	-- Any event
						data matches {
							ITEM_TREE[id4] matches {
								items matches {
									ELEMENT[id5] occurrences matches {0..1} matches {	-- Substance
										value matches {
											DV_TEXT[id20] 
										}
									}
									CLUSTER[id11] occurrences matches {0..1} matches {	-- Consumption details
										items matches {
											ELEMENT[id12] occurrences matches {0..1} matches {	-- Form
												value matches {
													DV_TEXT[id21] 
												}
											}
											ELEMENT[id19] occurrences matches {0..1} matches {	-- Method of use
												value matches {
													DV_TEXT[id22] 
												}
											}
											ELEMENT[id6] occurrences matches {0..1} matches {	-- Frequency
												value matches {
													DV_CODED_TEXT[id23] matches {
														defining_code matches {[ac1]}		-- Frequency
													}
												}
											}
											ELEMENT[id13] occurrences matches {0..*} 	-- Amount
										}
									}
								}
							}
						}
					}
				}
			}
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1"] = <
				text = <"Substance Use">
				description = <"Generic archetype to record the actual use or consumption of a defined substance at the present time, a specific time or over a period of time. ">
			>
			["id3"] = <
				text = <"Any event">
				description = <"Any event">
			>
			["id5"] = <
				text = <"Substance">
				description = <"Identification of substance ">
			>
			["id6"] = <
				text = <"Frequency">
				description = <"Frequency of use of substance">
			>
			["at7"] = <
				text = <"Daily use">
				description = <"Used every day">
			>
			["at8"] = <
				text = <"Weekly use">
				description = <"Used at least weekly">
			>
			["at9"] = <
				text = <"Irregular use">
				description = <"Used irregularly">
			>
			["at10"] = <
				text = <"No use">
				description = <"Not used at all">
			>
			["id11"] = <
				text = <"Consumption details">
				description = <"Details about the consumption of the substance">
			>
			["id12"] = <
				text = <"Form">
				description = <"Form of the substance used">
			>
			["id13"] = <
				text = <"Amount">
				description = <"Amount of substance consumed.  Data type can be further specified in specialised archetypes or in templates">
			>
			["id19"] = <
				text = <"Method of use">
				description = <"Method of use">
			>
			["ac1"] = <
				text = <"Frequency">
				description = <"Frequency of use of substance">
			>
		>
	>
	value_sets = <
		["ac1"] = <
			id = <"ac1">
			members = <"at7", "at8", "at9", "at10">
		>
	>
