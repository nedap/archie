archetype (adl_version=2.0.5; rm_release=1.0.2)
	openEHR-EHR-CLUSTER.lab_test_panel.v1.0.0

language
	original_language = <[ISO_639-1::en]>

description
	custodian_namespace = <"org.openehr">
	custodian_organisation = <"openEHR Foundation <http://www.openEHR.org>">

	original_author = <
		["name"] = <"Ian McNicoll">
		["email"] = <"ian@freshehr.com">
		["organisation"] = <"freshEHR Clinical Informatics, UK">
		["date"] = <"2014-04-18">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Parent for flattening tests on lab-style archetypes">
			keywords = <"ADL", "flattening", "test">
		>
	>
	lifecycle_state = <"published">
	other_details = <
		["regression"] = <"PASS">
	>
	copyright = <"Copyright © 2014 openEHR Foundation <http://www.openEHR.org>">
	licence = <"Creative Commons CC-BY-SA <https://creativecommons.org/licenses/by-sa/3.0/>">

definition
	CLUSTER[id1] matches {	-- Laboratory test panel
		items matches {
			CLUSTER[id3] matches {	-- Laboratory Result
				items matches {
					ELEMENT[id2] occurrences matches {0..1} 	-- Result Value
					ELEMENT[id4] matches {	-- Result Comment
						value matches {
							DV_TEXT[id15] 
						}
					}
					ELEMENT[id5] occurrences matches {0..1} matches {	-- Reference Range Guidance
						value matches {
							DV_TEXT[id16] 
						}
					}
					ELEMENT[id6] occurrences matches {0..1} matches {	-- Result Value Status
						value matches {
							DV_CODED_TEXT[id17] matches {
								defining_code matches {[ac1]}		-- Result Value Status (synthesised)
							}
						}
					}
					ELEMENT[id7] occurrences matches {0..1} matches {	-- DateTime Result Value Status
						value matches {
							DV_DATE_TIME[id18] 
						}
					}
				}
			}
			allow_archetype CLUSTER[id14] matches {	-- Other Detail
				include
					archetype_id/value matches {/.*/}
			}
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1"] = <
				text = <"Laboratory test panel">
				description = <"Laboratory test results as a single value or in a panel/battery format common to clinical pathology testing.">
			>
			["id2"] = <
				text = <"Result Value">
				description = <"Actual value of the result.">
			>
			["id3"] = <
				text = <"Laboratory Result">
				description = <"Specific detailed result, including both the value of the result item, and additional information that may be useful for clinical interpretation.">
			>
			["id4"] = <
				text = <"Result Comment">
				description = <"Comment about the Result.">
			>
			["id5"] = <
				text = <"Reference Range Guidance">
				description = <"Additional advice on the applicability of the reference range.">
			>
			["id6"] = <
				text = <"Result Value Status">
				description = <"The status of the result value.">
			>
			["id7"] = <
				text = <"DateTime Result Value Status">
				description = <"The date and/or time that the entire result was issued for the recorded ‘Result value status’.">
			>
			["at8"] = <
				text = <"Registered">
				description = <"No result yet available.">
			>
			["at9"] = <
				text = <"Interim">
				description = <"This is an initial or interim result: data may be missing or verification not been performed.">
			>
			["at10"] = <
				text = <"Final">
				description = <"The result is complete and verified by the responsible pathologist.">
			>
			["at11"] = <
				text = <"Amended">
				description = <"The result has been modified subsequent to being Final, and is complete and verified by the responsible pathologist.">
			>
			["at12"] = <
				text = <"Cancelled/Aborted">
				description = <"The result is unavailable because the test was not started or not completed.">
			>
			["at13"] = <
				text = <"Not requested">
				description = <"The result is not available as the test was not requested.">
			>
			["id14"] = <
				text = <"Other Detail">
				description = <"Further details including the specimen for the panel or a further nested panel.">
			>
			["ac1"] = <
				text = <"Result Value Status (synthesised)">
				description = <"The status of the result value. (synthesised)">
			>
		>
	>
	value_sets = <
		["ac1"] = <
			id = <"ac1">
			members = <"at8", "at9", "at10", "at11", "at12", "at13">
		>
	>
