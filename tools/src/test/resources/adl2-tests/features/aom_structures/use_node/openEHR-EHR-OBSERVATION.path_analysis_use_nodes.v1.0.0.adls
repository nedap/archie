archetype (adl_version=2.0.5; rm_release=1.0.2)
	openEHR-EHR-OBSERVATION.path_analysis_use_nodes.v1.0.0

language
	original_language = <[ISO_639-1::en]>

description
	custodian_namespace = <"org.openehr">
	custodian_organisation = <"openEHR Foundation <http://www.openEHR.org>">

	original_author = <
		["name"] = <"Thomas Beale">
 		["email"] = <"thomas.beale@openEHR.org">    
		["organisation"] = <"openEHR Foundation <http://www.openEHR.org>">
		["date"] = <"2004-06-01">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Illustrate path set created by multiple use_node references. Three paths should be visible that end in [id14] in the path analysis.">
			keywords = <"ADL", "test", "internal references">
		>
	>

	lifecycle_state = <"published">
	other_details = <
		["regression"] = <"PASS">
	>
	copyright = <"Copyright Â© 2004 openEHR Foundation <http://www.openEHR.org>">
	licence = <"Creative Commons CC-BY-SA <https://creativecommons.org/licenses/by-sa/3.0/>">

definition
	OBSERVATION[id1] matches {	-- Laboratory result
		data matches {
			HISTORY[id2] matches {
				events cardinality matches {1..*; unordered} matches {
					EVENT[id3] occurrences matches {0..*} matches {	-- Any event
						data matches {
							ITEM_TREE[id4] matches {
								items matches {
									CLUSTER[id12] matches {	-- level 1
										name matches {
											DV_CODED_TEXT[id23] matches {
												defining_code matches {[ac1]}		-- =LOINC::Battery_name
											}
										}
										items matches {
											CLUSTER[id13] matches {	-- level 2
												name matches {
													DV_CODED_TEXT[id24] matches {
														defining_code matches {[ac2]}		-- =LOINC::Battery_name
													}
												}
												items matches {
													use_node ELEMENT[id21] /data[id2]/events[id3]/data[id4]/items[id14]	-- /data[id2]/events[Any event]/data[id4]/items[Any result]
												}
											}
											use_node ELEMENT[id22] /data[id2]/events[id3]/data[id4]/items[id14]	-- /data[id2]/events[Any event]/data[id4]/items[Any result]
										}
									}
									ELEMENT[id14] 	-- Any result
								}
							}
						}
					}
				}
			}
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1"] = <
				text = <"Laboratory result">
				description = <"Generic laboratory result archetype to allow terminology driven results on a generic framework">
			>
			["id3"] = <
				text = <"Any event">
				description = <"Any event in a time series">
			>
			["id12"] = <
				text = <"level 1">
				description = <"A cluster to enable level one battery labels">
			>
			["id13"] = <
				text = <"level 2">
				description = <"A cluster to enable level 2 battery labels">
			>
			["id14"] = <
				text = <"Any result">
				description = <"Generic result - data type is not set">
			>
			["id21"] = <
				text = <"ref 1">
				description = <"Use node ref #1">
			>
			["id22"] = <
				text = <"ref 2">
				description = <"Use node ref #2">
			>
			["ac1"] = <
				text = <"=LOINC::Battery_name">
				description = <"Any name for a set of tests from the LOINC set of battery names">
			>
			["ac2"] = <
				text = <"=LOINC::Battery_name">
				description = <"Any name for a set of tests from the LOINC set of battery names">
			>
		>
	>
