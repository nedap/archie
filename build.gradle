buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    classpath 'ch.raffael.pegdown-doclet:pegdown-doclet:1.3'
    classpath 'org.owasp:dependency-check-gradle:1.4.5.1'
  }
}

repositories {
  mavenCentral()
}


wrapper {
    gradleVersion = '4.10'
}

allprojects {
  version = '0.5.2'
  group = 'com.nedap.healthcare.archie'
  ext.gradleScriptDir = "${rootProject.projectDir}/gradle"
  //archivesBaseName = 'archie'
}

gradle.ext.ossrhUsernameSafe = hasProperty('ossrhUsername') ? ossrhUsername : ""
gradle.ext.ossrhPasswordSafe = hasProperty('ossrhPassword') ? ossrhPassword : ""
gradle.ext.shouldSign = hasProperty('signing.keyId')
gradle.ext.isParallel = hasProperty('org.gradle.parallel') ? project.property('org.gradle.parallel') == 'true' : false


subprojects {
  apply plugin: 'java'
  apply plugin: 'ch.raffael.pegdown-doclet'
  apply plugin: 'org.owasp.dependencycheck'
 // apply from: "${gradleScriptDir}/jacoco.gradle"  
  apply from: "${gradleScriptDir}/publish-maven.gradle"  

 // plugins {
   //   id 'com.github.ben-manes.versions' version '0.13.0'
 // }

  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'

  compileJava.options.encoding = "UTF-8"
  compileTestJava.options.encoding = "UTF-8"

  repositories {
      mavenCentral()
  }

  ext.reflectionsVersion = '0.9.11'
  ext.jacksonVersion = '2.9.6'

  dependencies {
    compile 'org.slf4j:slf4j-api:1.7.25'

    compile "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"

    compile 'com.google.guava:guava:25.1-jre'

    compile "org.reflections:reflections:${reflectionsVersion}"
    compile 'com.esotericsoftware:kryo-shaded:4.0.2' //for easy and relatively fast object cloning

    compile('commons-io:commons-io:2.6'){
      exclude group: 'commons-logging', module: 'commons-logging'
    }
    compile('commons-lang:commons-lang:2.6')

    //java 10 no longer has these included by default, so explicit dependency is needed.
    compile 'javax.xml.bind:jaxb-api:2.3.0'
    compile 'com.sun.xml.bind:jaxb-core:2.3.0'
    compile 'com.sun.xml.bind:jaxb-impl:2.3.0'
    compile 'javax.activation:activation:1.1.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.slf4j:slf4j-simple:1.7.25'
  }


  javadoc {
      options.encoding = 'UTF-8'
  }

  //configure the jar files: sources and javaod
  task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
  }

  task sourcesJar(type: Jar) {
      classifier = 'sources'
      from sourceSets.main.allSource
  }

  artifacts {
      archives javadocJar, sourcesJar
  }

  test {
   testLogging {
      events "failed"
      exceptionFormat "full"      
    }
  }

}







